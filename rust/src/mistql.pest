query = { SOI ~ expression ~ EOI }

expression = {
    value |
    monad
}

monad = { monadic_op ~ expression }

monadic_op = { "!" }

value = { at | literal }
at = { "@" }

literal = { object | array | string | number | boolean | null }

object = {
    "{" ~ "}" |
    "{" ~ keyval ~ ("," ~ keyval)* ~ "}"
}
keyval = { string ~ ":" ~ expression }

array = {
    "[" ~ "]" |
    "[" ~ expression ~ ("," ~ expression)* ~ "]"
}

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

boolean = { "true" | "false" }

null = { "null" }

WHITESPACE = _{ " " }